changes from 0.2
- rename `Prepare a` to `NetworkDescription a`
- extend `AddHandler` to return an unsubscription function
  - `type AddHandler a = (a -> IO ()) -> IO (IO ())`
- add a `pause` action to the compiled network
  - from `prepareEvents :: NetworkDescription () -> IO ()`
  - to `compile :: NetworkDescription () -> IO EventNetwork`
    - `EventNetwork {..}`
      - `actuate :: IO ()`
      - `pause :: IO ()`
- add `fromPoll` to internalize external state variable as `Behavior`
  - `fromPoll :: IO a -> NetworkDescription (Model.Behavior PushIO a)`
- rename `compileAccumB` to `complieReadBehavior`
  - `compileReadBehavior` -> `compileUnion` -> `map compilePath`

## Backend (PushIO)

```haskell

data PushIO

-- type Event = Model.Event PushIO
newtype instance Model.Event PushIO a = Event (Event Accum a)

-- type Behavior = Model.Behavior PushIO
newtype instance Model.Behavior PushIO a = Behavior (Behavior Accum a)

------
-- AST

type family   Event t a
type instance Event Accum  a = (Ref (EventStore a), EventD Accum a)
type instance Event Shared a = (Ref (EventStore a), EventD Shared a)
type instance Event Linear a = EventD Linear a

type family   Behavior t a
type instance Behavior Accum  a = (Ref (BehaviorStore a), BehaviorD Accum a)
type instance Behavior Shared a = (Ref (BehaviorStore a), BehaviorD Linear a)
type instance Behavior Linear a = (Ref (BehaviorStore a), BehaviorD Linear a)

data Accum
data Shared
data Linear

type Ref a = IORef (Maybe a)
type EventStore a = [(Channel, CacheRef a)]
type BehaviorStore a = BehaviorRef a

-- NOTE type Cache = [IO ()]
data Cache = Cache
  { vault :: Vault
  , initializers :: [VaultChanger]
  , finalizers   :: [VaultChanger]
  }
type VaultChanger = Run () -- StateT Vault IO ()

-- A simple value to be cached. Lasts one phase. Useful for sharing.
-- NOTE type CacheRef a = IORef (Maybe  a)
type CacheRef a = Vault.Key a

-- Cache and accumulate a value over several phases.
-- NOTE type AccumRef a = IORef a
type AccumRef a = Vault.Key a

-- Cache and accumulate a value over several phases,
-- but updates are only visible at the beginning of a new phase.
-- NOTE type BehaviorRef a = (IORef a, IORef a)
type BehaviorRef a = (AccumRef a, CacheRef a)

data EventD t a where
  Filter    :: (a -> Bool) -> Event t a -> EventD t a
  ApplyE    :: forall i. Behavior t (i -> a) -> Event t i -> EventD t a
  AccumE    :: a -> Event t (a -> a) -> EventD t a
  Union     :: Event t a -> Event t a -> EventD t a
  Never     :: EventD t a

  -- internal combinators
  -- Source
  Input         :: Typeable a => Channel -> EventD t a
  -- Sink
  Reactimate    :: Event t (IO ()) -> EventD t ()

  ReadCache     :: Channel -> CacheRef a -> EventD t a
  WriteCache    :: CacheRef a -> Event t a -> EventD t a

  UpdateAccum   :: AccumRef a -> Event t (a -> a) -> EventD t a
  -- NOTE renamed from WriteBehavior
  UpdateBehavior :: forall i. BehaviorRef i -> Event t (i -> i) -> EventD t ()

data BehaviorD t a where
  Pure         :: a -> BehaviorD t a
  ApplyB       :: forall i. Behavior t (i -> a) -> Behavior t i -> BehaviorD t a
  AccumB       :: a -> Event t (a -> a) -> BehaviorD t a
  -- Source
  Poll         :: IO a -> BehaviorD t a -- NOTE new

  -- internal combinators
  ReadBehavior :: BehaviorRef a -> BehaviorD t a

```

## Source

2 initial actions
- `Input :: Channel -> EventD t a`
- `Poll :: IO a -> BehaviorD t a`

```haskell
-- external system communicates state transitions to program by callback functions
-- from program's perspective, external system samples its state variables into Event steams which likely contain incremental changes
fromAddHandler
  :: Typeable a
  => AddHandler a -> NetworkDescription (Model.Event PushIO a)
fromAddHandler addHandler = Prepare $ do
        channel <- newChannel
        let addHandler' k = addHandler $ k . toUniverse channel
        tell ([], [(channel, addHandler')])
        return $ input channel
          where
            input :: Typeable a => Channel -> Model.Event PushIO a
            input = event . Input
              where
                event :: EventD Accum a -> Model.Event PushIO a
                event e = Event pair
                  where
                    -- mention argument to prevent let-floating
                    pair = unsafePerformIO (fmap (,e) newRef)
    where
      newChannel = do c <- get; put $! c+1; return c

-- external system exposes its state variables directly accessible (through an IO action) by the program
-- program needs to take the responsibility of sampling in order to response to its changes
-- the sampling is still driven by Event steams from external system, e.g. user actions, global clock ticks
fromPoll :: IO a -> NetworkDescription (Model.Behavior PushIO a)
fromPoll m = return $ poll m
  where
    poll :: IO a -> Model.Behavior PushIO a
    poll = behavior . Poll
      where
        -- sharing
        behavior :: BehaviorD Accum a -> Model.Behavior PushIO a
        behavior b = Behavior pair
          where
            -- mention argument to prevent let-floating
            pair = unsafePerformIO (fmap (,b) newRef)
```

## Compile

- 3-stage transpiling
  ```haskell
  Event Accum () -> Event Shared () -> [Event Linear a] -> [Path]
  ```
- compilation
  ```haskell
  Compile [Path] -> IO ([Path], Cache)
  ```

```haskell
-- store monad
type Store = IO

-- monad to build the network in
type Compile a = StateT Cache Store a

-- monad to run the network in
-- NOTE type Run = IdentityT IO
type Run = StateT Vault IO

compile
  :: Event Accum () -- Program, discrete machine with Accum backend
  -> IO ([Path], Cache)
compile e
  = runStore
  $ runCompile
  $ return . map compilePath
  =<< compileUnion
  =<< compileReadBehavior
    $ e
```

### 1. compileReadBehavior

```haskell
-- allocated caches for accumulated (accumB) and external behaviors (fromPoll),
-- turn them into reads from the cache
-- NOTE renamed from CompileAccumB
type CompileReadBehavior = WriterT [Event Shared ()] Compile

compileReadBehavior :: Event Accum () -> Compile (Event Shared ())
compileReadBehavior e1 = do
  (e :: Event Shared (), es :: [Event Shared ()]) <- runWriterT (goE e1)
  -- include updates to Behavior as additional events
  let union e1 e2 = (invalidRef, Union e1 e2)
  return $ foldr1 union (e:es)
    where
    -- boilerplate traversal for events
    -- NOTE only thing happening here is recursively set phantom type t = Shared
    goE :: Event Accum a -> CompileReadBehavior (Event Shared a)
    goE (ref, Filter p e )      = (ref,) <$> (Filter p   <$> goE e)
    goE (ref, Union e1 e2)      = (ref,) <$> (Union      <$> goE e1 <*> goE e2)
    goE (ref, ApplyE b e )      = (ref,) <$> (ApplyE     <$> goB b  <*> goE e )
    goE (ref, AccumE x e )      = (ref,) <$> (AccumE x   <$> goE e)
    goE (ref, Reactimate e)     = (ref,) <$> (Reactimate <$> goE e)
    goE (ref, Never)            = (ref,) <$> (pure Never)
    goE (ref, Input c)          = (ref,) <$> (pure $ Input c)

    -- almost boilerplate traversal for behaviors
    -- NOTE set t = Shared, except Poll and AccumB
    goB :: Behavior Accum a -> CompileReadBehavior (Behavior Shared a)
    goB (ref, Pure x      ) = (ref,) <$> (Pure   <$> return x)
    goB (ref, ApplyB bf bx) = (ref,) <$> (ApplyB <$> goB bf <*> goB bx)
    -- NOTE fromPoll
    goB (ref, Poll io     ) = (ref,) <$> (ReadBehavior <$> makeRef)
      -- ReadBehavior :: BehaviorRef a -> BehaviorD Linear a
      where
        makeRef :: CompileReadBehavior (BehaviorRef a)
        makeRef = do
          m <- lift . lift $ readRef ref
          case m of
            Just r  -> return r
            Nothing -> do
              r <- lift $ newBehaviorRefPoll io
              lift . lift $ writeRef ref r
              return r
                where
                  newBehaviorRefPoll :: IO a -> Compile (BehaviorRef a)
                  newBehaviorRefPoll = newBehaviorRef . liftIO

                  newBehaviorRef :: Compile a -> Compile (BehaviorRef a)
                  newBehaviorRef m = do
                    temp <- newCacheRef
                    registerInitializer $ writeCacheRef temp =<< m
                    return (undefined, temp)
                      where
                        newCacheRef :: Compile (CacheRef a)
                        newCacheRef = do
                          key <- liftIO $ Vault.newKey
                          registerFinalizer
                            $ put
                            =<< liftIO . Vault.delete key
                            =<< get
                          return key

                        writeCacheRef :: CacheRef a -> a -> Run ()
                        writeCacheRef = writeVaultKey

                        registerInitializer :: VaultChanger -> Compile ()
                        registerInitializer m = modify \cache ->
                          cache { initializers = cache.initializers ++ [m] }

                        registerFinalizer :: VaultChanger -> Compile ()
                        registerFinalizer m = modify \cache ->
                          cache { finalizers = cache.finalizers ++ [m] }
    -- NOTE accumB
    goB (ref, AccumB x e  ) = (ref,) <$> (ReadBehavior <$> makeRef)
      where
        makeRef :: CompileReadBehavior (BehaviorRef a)
        makeRef = do
          m <- lift . lift $ readRef ref
          case m of
            Just r  -> return r
            Nothing -> do
              -- create new BehaviorRef and share it
              r <- lift $ newBehaviorRefAccum x
              lift . lift $ writeRef ref r

              -- remove  accumB  from the other events
              e <- goE e
              tell [(invalidRef, UpdateBehavior r e)] -- NOTE WriterT [Event Shared ()]
              return r
                where
                  newBehaviorRefAccum :: a -> Compile (BehaviorRef a)
                  newBehaviorRefAccum x = do
                    acc  <- newAccumRef x
                    (_,temp) <- newBehaviorRef $ readAccumRef acc
                    return (acc, temp)
                      where
                        readAccumRef :: AccumRef a -> Run a
                        readAccumRef ref = fromJust <$> readVaultKey ref

                        newBehaviorRef :: Compile a -> Compile (BehaviorRef a)

                  invalidRef :: Ref (EventStore a)
                  invalidRef = error "Store: invalidRef. This is an internal bug."

```
